apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

def libraryVersion = (System.getenv("VERSION_NAME") ?: "0.1.0").replaceFirst("^v", "")
def githubOwner = "UnifiedPaymentSolutions"
def githubRepo = "googlepay-sdk"

android {
    namespace 'com.everypay.gpay'
    compileSdkVersion 36

    defaultConfig {
        minSdk 24

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        // Allow this library to be used by apps with product flavors
        missingDimensionStrategy 'standard', 'production'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.15.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.android.material:material:1.13.0'

    // Google Pay
    implementation 'com.google.android.gms:play-services-wallet:19.4.0'

    // HTTP Client for EveryPay API
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'

    // Coroutines for async operations
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'

    // Testing
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'io.mockk:mockk:1.13.13'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.12.0'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.10.1'
    testImplementation 'com.google.truth:truth:1.4.4'
    testImplementation 'app.cash.turbine:turbine:1.2.0'
    testImplementation 'org.json:json:20240303'
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = 'com.everypay'
                artifactId = 'gpay'
                version = libraryVersion

                pom {
                    name = 'EverypayGooglePay'
                    description = 'Google Pay SDK for Everypay payment processing'
                    url = "https://github.com/${githubOwner}/${githubRepo}"

                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }

                    developers {
                        developer {
                            id = 'everypay'
                            name = 'Everypay'
                            email = 'support@every-pay.com'
                        }
                    }

                    scm {
                        connection = "scm:git:git://github.com/${githubOwner}/${githubRepo}.git"
                        developerConnection = "scm:git:ssh://github.com/${githubOwner}/${githubRepo}.git"
                        url = "https://github.com/${githubOwner}/${githubRepo}"
                    }
                }
            }
        }

        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/${githubOwner}/${githubRepo}")
                credentials {
                    username = System.getenv("GITHUB_ACTOR") ?: project.findProperty("gpr.user") ?: ""
                    password = System.getenv("GITHUB_TOKEN") ?: project.findProperty("gpr.token") ?: ""
                }
            }
        }
    }
}